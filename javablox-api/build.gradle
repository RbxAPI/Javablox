plugins {
    id 'java-library'
    id 'org.gradle.java.experimental-jigsaw' version "0.1.1"
    id 'maven-publish'
    id 'signing'
}

javaModule.name = 'io.github.rbxapi.javablox.api'

group 'io.github.rbxapi'
version '1.0-alpha-1'

sourceCompatibility = 1.11

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}


publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'javablox-api'
            from components.java
            artifact sourcesJar
            artifact javadocJar
            pom {
                name = 'Javablox API'
                description = 'API interfaces for Javablox'
                url = 'https://github.com/rbxapi/javablox'
                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://github.com/RbxAPI/Javablox/blob/master/LICENSE.md'
                    }
                }
                developers {
                    developer {
                        id = 'ChingDim'
                        name = 'ChingDim'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/RbxAPI/Javablox.git'
                    developerConnection = 'scm:git:ssh://github.com/RbxAPI/Javablox.git'
                    url = 'http://github.com/RbxAPI/Javablox/'
                }
            }
        }
    }
    repositories {
        maven {
            credentials {
                username "$mavenUser"
                password "$mavenPassword"
            }
            def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2"
            def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}


javadoc {
    if (JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}

// Don't add wrapper because it is included in Javablox